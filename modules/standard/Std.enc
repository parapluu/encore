module Std

-- This is currenly just a marker interface. In the future, id
-- might contain the logic for giving objects ids.
read trait Id
end

-- Equivalence is currently only supported on a given type. In
-- the future, other solutions are planned, possibly including a
-- self type instead of this type parameter.
read trait Eq[self]
  require def eq(other:self) : bool
end

-- A trait for comparable objects.
-- Result: 0 is equal, 1 is this is greater, -1 other is greater
read trait Ord[self]
  require def compare(other:self) : int
end

-- The identity function
fun id[a](x : a) : a
  x
end

-- The default trait included by all active classes
active trait Actor
  def await[t](f : Fut[t]) : unit
    EMBED (unit)
      future_await(_ctx, #{f});
    END
  end
  def suspend() : unit
    EMBED (unit)
      actor_suspend(_ctx);
    END
  end
  def run_attached_closure[t](f : local(() -> t)) : t
    f()
  end
end