## A systemTap probing tool for Encore that prints human readable data
## Written by Joy van den EIjkhof, Ardalan Samimi, Ulf Sigvardsson
## March 2019

## Counters
## Contains how many times each probe has been called
## Uses aggregation
global actor_msg_send
global future_create
global future_block
global future_destroy
global future_fulfil_start
global future_fulfil_end
global future_get
global future_unblock
global future_chaining
global actor_msg_run
global successful_steals
global failed_steals
global total_steals


#Lists containing data corresponding to their name
#Each list corresponds to one foreach-loop in probe end

#List for counting lifetimes
global list_future_lifetime
global list_future_block_lifetime

#How many times does an Actor get blocked by any Future
global actor_block_count

#How many times does what Actor call get() on what future
global actor_get_future

#How many time does a Future block any actor
global future_block_count

#How many times is a Future chained at what Actor
global chained_actor_future_list

#How many time a Scheduler successfully steals work any other Scheduler 
global successful_steals_count

#How many times an Actor is stolen from any Scheduler
global actor_stolen

#How many time a Scheduler fails to steals work any other Scheduler 
global failed_steals_count

#What Future blocks what Future
global future_block_actor

#What Scheduler steals from what Scheduler sucess/fail
global scheduler_from_scheduler
global scheduler_from_scheduler_fail

#Counts how many core-swites have been made
global core_switch

#Keeps track of what scheduler is used by what core.
global current_sched_core



#messages
probe process.mark("actor-msg-send") {
      #Count
      actor_msg_send <<< 1;
}

probe process.mark("actor-msg-run") {
      #Count
      actor_msg_run <<< 1;
}

probe process.mark("work-steal-successful") {
      #Count
      successful_steals <<< 1;
      total_steals <<< 1;

      #Arguments from probe
      scheduler = sprint($arg1)
      victim = sprint($arg2)
      actor = sprint($arg3)

  
      successful_steals_count[scheduler] <<< 1
      actor_stolen[actor] <<< 1
      scheduler_from_scheduler[scheduler, victim] <<< 1; 

      if (cpu() != current_sched_core[scheduler]) {
      	 	core_switch <<< 1;
		current_sched_core[scheduler] = cpu()
      }
            					  
}

probe process.mark("work-steal-failure") {
      #Count
      failed_steals <<< 1;
      total_steals <<< 1;

      #Arguments from probe
      scheduler = sprint($arg1)
      victim = sprint($arg2) 

      failed_steals_count[scheduler] <<< 1
      scheduler_from_scheduler_fail[scheduler, victim] <<< 1
            					  
}


probe process.mark("future-create") {
      #Count
      future_create <<< 1;

      #Arguments from probe
      future = sprint($arg2)
      
      list_future_lifetime[future] = gettimeofday_ns()
}


probe process.mark("future-destroy") {
      #Count
      future_destroy <<< 1;

      #Arguments
      future = sprint($arg2)
      
      list_future_lifetime[future] = gettimeofday_ns()-list_future_lifetime[future]
}

probe process.mark("future-block") {
      #Count
      future_block <<< 1;

      #Arguments from probe
      actor = sprint(@cast($arg1, "pony_ctx_t")->current)
      future = sprint($arg2)

      actor_block_count[actor] <<< 1      
      future_block_actor[future, actor] <<< 1
      future_block_count[future] <<< 1

      list_future_block_lifetime[future, actor] = gettimeofday_ns()
      
}

probe process.mark("future-unblock") {
      #Count
      future_unblock <<< 1;

      #Arguments from probe
      actor = sprint(@cast($arg1, "pony_ctx_t")->current)
      future = sprint($arg2)
      
      list_future_block_lifetime[future, actor] = gettimeofday_ns()-list_future_block_lifetime[future, actor]
}

probe process.mark("future-get") {
     #Count
     future_get <<< 1;

     #Arguments from probe
     actor = sprint(@cast($arg1, "pony_ctx_t")->current)
     future = sprint($arg2)

     actor_get_future[actor, future] <<< 1;
     
}

probe process.mark("future-fulfil-end"){
      #Count
      future_fulfil_end <<< 1;
}

probe process.mark("future-fulfil-start"){
      #Count
      future_fulfil_start <<< 1;
}

probe process.mark("future-chaining") {
      #Count
      future_chaining <<< 1;

      #Arguments from probe
      future_chained = sprint($arg2)
      actor_chained = sprint($arg1)
      
      chained_actor_future_list[actor_chained, future_chained] <<< 1;
}

probe end {
      print("--- DATA FROM PROBING ---")
      print("\n--- COUNTS ---\n")
      ams = @count(actor_msg_send)
      fc = @count(future_create)
      fb = @count(future_block)
      ffs = @count(future_fulfil_start)
      ffe = @count(future_fulfil_end)
      fd = @count(future_destroy)
      fg = @count(future_get)
      fu = @count(future_unblock)
      fch = @count(future_chaining)
      amr = @count(actor_msg_run)
      ss =  @count(successful_steals)
      fs =  @count(failed_steals)
      ts =  @count(total_steals)
      
      printf("future-chaining\t\t%d\n", fch)
      printf("future-block\t\t%d\n", fb)
      printf("future-create\t\t%d\n", fc)
      printf("future-destroy\t\t%d\n", fd)
      printf("future-fulfil-start\t%d\n", ffs)
      printf("future-fulfil-end\t%d\n", ffe)
      printf("future-get\t\t%d\n", fg)
      printf("future-unblock\t\t%d\n", fu) 
      printf("actor-msg-send\t\t%d\n", ams)
      printf("actor-msg-run\t\t%d\n", amr)
      printf("work-steal-failure\t\t%d\n", fs)
      printf("work-steal-successful\t\t%d\n", ss)
      printf("work-steal-attempt\t\t%d\n", ts)
      printf("core-switches:\t%d\n", @count(core_switch))


      print("\n--- LIFETIME OF FUTURE ---\n")
      print("Future Addr\t\tLifetime (nanoseconds)\n")
      foreach(fut in list_future_lifetime) {
         printf("%s\t\t%d\n", fut, list_future_lifetime[fut])

      }


      print("\n--- LIFETIME OF A FUTURE BLOCK ---\n")
      print("Future Addr\t\tActor Addr\t\tLifetime (nanoseconds)\n")
      foreach([fut, act] in list_future_block_lifetime) {
         printf("%s\t\t%s\t\t%d\n", fut, act, list_future_block_lifetime[fut, act])

      }

      print("\n---NUMBER OF TIMES ACTOR CALLS GET ON FUTURE---\n")
      print("Actor addr\t\tFuture Addr\t\tCount\n")
      foreach ([act, fut] in actor_get_future) {
      	      printf("%s\t\t%s\t\t%d\n", act, fut, @count(actor_get_future[act, fut]))
      }


      print("\n--- WHAT FUTURE BLOCKED WHAT ACTOR ---\n")
      print("Future Addr\t\tActorr Addr\t\tCount\n")
      foreach([fut, actor] in future_block_actor) {	
      	printf("%s\t\t%s\t\t%d\n", fut, actor, @count(future_block_actor[fut, actor]))	  
      }

      
      print("\n--- NUMBER OF TIMES AN ACTOR IS BLOCKED ---\n")
      print("Actor Addr\t\tCount\n")
      foreach(act in actor_block_count) {
        printf("%s\t\t%d\n", act, @count(actor_block_count[act]))
      }

  
      print("\n--- NUMBER OF TIMES A FUTURE BLOCKS ---\n")
      print("Future Addr\t\tCount\n")
      foreach(fut in future_block_count) {
        printf("%s\t\t%d\n", fut, @count(future_block_count[fut]))
      }

      print("\n--- WHAT FUTURES CHAINES AT WHAT ACTOR ---\n")
      print("Actor Addr\t\tFuture Addr\t\tCount\n")
      foreach([act, fut] in chained_actor_future_list) {
        printf("%s\t\t%s\t\t%d\n",act, fut, @count(chained_actor_future_list[act, fut]))
      }

      print("\n---SUCCESSFUL STEALS---\n")
      print("Scheduler\t\tCount\n")
      foreach(ssid in successful_steals_count) {
       	 printf("%s\t\t%d\n", ssid, @count(successful_steals_count[ssid]))
      }

      print("\n---FAILED STEALS---\n")
      print("Scheduler\t\tCount\n")
      foreach(fsid in failed_steals_count) {
        printf("%s\t\t%d\n", fsid, @count(failed_steals_count[fsid]))
      }

      print("\n---STEALS BETWEEN SCHEDULERS---\n")
      print("Stolen by\t\tStolen from\t\tCount\n")
      foreach([steal, victim] in scheduler_from_scheduler) {
      	printf("%s\t\t%s\t\t%d\n", steal, victim, @count(scheduler_from_scheduler[steal, victim]))
      }

      print("\n---FAILS BETWEEN SCHEDULERS---\n")
      print("Attempted by\t\tTarget\t\t\tCount\n")
      foreach([steal, victim] in scheduler_from_scheduler_fail) {
      	printf("%s\t\t%s\t\t%d\n", steal, victim, @count(scheduler_from_scheduler_fail[steal, victim]))
      }

      print("\n---STOLEN ACTORS---\n")
      print("Actor ID\t\tTimes Stolen\n")
      foreach(actor in actor_stolen) {
         printf("%s\t\t%d\n", actor,  @count(actor_stolen[actor]))
      }
      
}