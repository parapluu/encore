import Random
import ArrayList
typedef Position = int
fun left() : Position
  -(1)
end
fun right() : Position
  1
end
fun initial() : Position
  0
end
fun filterLessThan(data : borrowed ArrayList[int], pivot : int) : ArrayList[int]
  var result = new ArrayList[int](data.size())
  var i = 0
  while i < data.size() do
    let
      d = data.at(i)
    in
      if d < pivot then
        result.add(d)
      end
      i = i + 1
    end
  end
  consume result
end
fun filterEqualsTo(data : borrowed ArrayList[int], pivot : int) : ArrayList[int]
  var result = new ArrayList[int](data.size())
  var i = 0
  while i < data.size() do
    let
      d = data.at(i)
    in
      if d == pivot then
        result.add(d)
      end
      i = i + 1
    end
  end
  consume result
end
fun filterGreaterThan(data : borrowed ArrayList[int], pivot : int) : ArrayList[int]
  var result = new ArrayList[int](data.size())
  var i = 0
  while i < data.size() do
    let
      d = data.at(i)
    in
      if d > pivot then
        result.add(d)
      end
      i = i + 1
    end
  end
  consume result
end

fun quicksortSeq(data : borrowed ArrayList[int]) : ArrayList[int]
  let
    dataLength = data.size()
  in
    var result = new ArrayList[int](dataLength)
    if dataLength < 2 then
      result.addAll(data)
      consume result
    else
      val pivot = data.at(data.size() / 2)
      var leftUnsorted = filterLessThan(data, pivot)
      var leftSorted = quicksortSeq(leftUnsorted)
      var equalElements = filterEqualsTo(data, pivot)
      var rightSorted = quicksortSeq(filterGreaterThan(data, pivot))
      result.addAll(leftSorted)
      result.addAll(equalElements)
      result.addAll(rightSorted)
      assertTrue(result.size() == data.size(), "the size of the resulting array is different from the size of the input")
      consume result
    end
  end
end
fun checkSorted(data : ArrayList[int]) : bool
  true
end
fun randomInitialArray(n : int, s : int, m : int) : ArrayList[int]
  var result = new ArrayList[int](n)
  val r = new Random(s)
  for i <- [0..n - 1] do
    result.add(r.random(m))
  end
  consume result
end
fun printArray(s : String, a : borrowed ArrayList[int]) : unit
  if a.size() > 0 then
    var result = string_from_int(a.at(0))
    var i = 1
    while i < a.size() do
      result = result.concatenate(", ")
      result = result.concatenate(string_from_int(a.at(i)))
      i = i + 1
    end
    print("{}: {} \n", s, result)
  end
end

active class QuickSortActor : Id
  var t : int
  var parent : QuickSortActor
  var result : ArrayList[int]
  var numFragments : int
  var myPosition : Position
  def init(parent : QuickSortActor, pos : Position, t : int, n : int) : unit
    this.t = t
    this.parent = parent
    this.result = new ArrayList[int](n)
    this.numFragments = 0
    this.myPosition = pos
  end
  def done(var data : ArrayList[int], position : Position) : unit
    if data != null then
      if position == left() then
        data.addAll(consume this.result)
        this.result = consume data
      else
        if position == right() then
          this.result.addAll(data)
        end
      end
      this.numFragments = this.numFragments + 1
      if this.numFragments == 3 then
        this.notify()
      end
    end
  end
  def notify() : unit
    if this.parent != null then
      this.parent!done(consume this.result, this.myPosition)
    else
      var res = consume this.result
--      printArray("result", res)
      this.result = consume res
    end
  end
  def sort(data : ArrayList[int]) : unit
    let
      dataLength = data.size()
    in
      if dataLength < this.t then
        this.result = quicksortSeq(data)
        this.notify()
      else
        let
          pivot = data.at(dataLength / 2)
        in
          (new QuickSortActor(this, left(), this.t, data.size()))!sort(filterLessThan(data, pivot))
          (new QuickSortActor(this, right(), this.t, data.size()))!sort(filterGreaterThan(data, pivot))
          this.result = filterEqualsTo(data, pivot)
          this.numFragments = this.numFragments + 1
        end
      end
    end
  end
end
active class Main
  def argToInt(str : String) : int
    match str.to_int() with
      case Just(result) =>
        result
      end
      case Nothing =>
        0
      end

    end
  end
  def main(args : [String]) : unit
    let
      n = 100 --1000000
      m = 1152921504606846976 -- 2^60
      t = 2048
      s = 1024
    in
      (new QuickSortActor(null, initial(), t, n))!sort(randomInitialArray(n, s, m))
      println("Done!")
    end
  end
end
