import Random
import ArrayList

typedef Position = int

fun left() : Position
  -(1)
end

fun right() : Position
  1
end

fun initial() : Position
  0
end
fun filterLessThan(arr : borrowed ArrayList[int], pivot : int) : ArrayList[int]
  var result = new ArrayList[int](arr.size())
  var i = 0
  while i < arr.size() do
    let
      d = arr.at(i)
    in
      if d < pivot then
        result.add(d)
      end
      i = i + 1
    end
  end
  consume result
end
fun filterEqualsTo(arr : borrowed ArrayList[int], pivot : int) : ArrayList[int]
  var result = new ArrayList[int](arr.size())
  var i = 0
  while i < arr.size() do
    let
      d = arr.at(i)
    in
      if d == pivot then
        result.add(d)
      end
      i = i + 1
    end
  end
  consume result
end
fun filterGreaterThan(arr : borrowed ArrayList[int], pivot : int) : ArrayList[int]
  var result = new ArrayList[int](arr.size())
  var i = 0
  while i < arr.size() do
    let
      d = arr.at(i)
    in
      if d > pivot then
        result.add(d)
      end
      i = i + 1
    end
  end
  consume result
end

fun quicksortSeq(arr : borrowed ArrayList[int]) : ArrayList[int]
  let
    arrLength = arr.size()
  in
    var result = new ArrayList[int](arrLength)
    if arrLength < 2 then
      result.addAll(arr)
      consume result
    else
      val pivot = arr.at(arr.size() / 2)
      var leftUnsorted = filterLessThan(arr, pivot)
      var leftSorted = quicksortSeq(leftUnsorted)
      var equalElements = filterEqualsTo(arr, pivot)
      var rightSorted = quicksortSeq(filterGreaterThan(arr, pivot))
      result.addAll(leftSorted)
      result.addAll(equalElements)
      result.addAll(rightSorted)
      assertTrue(result.size() == arr.size(), "the size of the resulting array is different from the size of the input")
      consume result
    end
  end
end
fun checkSorted(arr : ArrayList[int]) : bool
  true
end
fun randomInitialArray(n : int, s : int, m : int) : ArrayList[int]
  var result = new ArrayList[int](n)
  val r = new Random(s)
  for i <- [0..size - 1] do
    result.add(r.random(maxVal))
  end
  consume result
end

fun printArray(s : String, list : borrowed ArrayList[int]) : unit
  var n = list.size()

  if n > 0 then
    var result = string_from_int(list.at(0))
    var i = 1
    while i < n do
      result = result.concatenate(", ")
      result = result.concatenate(string_from_int(list.at(i)))
      i = i + 1
    end
    print("{}: {} \n", s, result)
  end
end

active class QuickSortActor : Id
  var seqThreshold : int
  var parent : QuickSortActor
  var result : ArrayList[int]
  var numFragments : int
  var myPosition : Position -- The position from the perspective of the parent, left or right part of original list

  def init(parent : QuickSortActor, pos : Position, seqThreshold : int, n : int) : unit
    this.seqThreshold = seqThreshold
    this.parent = parent
    this.result = new ArrayList[int](n)
    this.numFragments = 0 -- Number of completed fragments, done when 3
    this.myPosition = pos
  end
  def done(var arr : ArrayList[int], position : Position) : unit
    if arr != null then
      if position == left() then
        arr.addAll(consume this.result)
        this.result = consume arr
      else
        if position == right() then
          this.result.addAll(arr)
        end
      end
      this.numFragments = this.numFragments + 1
      if this.numFragments == 3 then
        this.notify()
      end
    end
  end

  def notify() : unit
    if this.parent != null then
      this.parent!done(consume this.result, this.myPosition)
    else
      var res = consume this.result
--      printArray("result", res)
      this.result = consume res
    end
  end
  def sort(arr : ArrayList[int]) : unit
    let
      arrLength = arr.size()
    in
      if arrLength < this.t then
        this.result = quicksortSeq(arr)
        this.notify()
      else
        let
          pivot = arr.at(arrLength / 2)
        in
          (new QuickSortActor(this, left(), this.t, arr.size()))!sort(filterLessThan(arr, pivot))
          (new QuickSortActor(this, right(), this.t, arr.size()))!sort(filterGreaterThan(arr, pivot))
          this.result = filterEqualsTo(arr, pivot)
          this.numFragments = this.numFragments + 1
        end
      end
    end
  end
end

active class Main
  def argToInt(str : String) : int
    match str.to_int() with
      case Just(result) =>
        result
      end
      case Nothing =>
        0
      end

    end
  end

  def main(args : [String]) : unit
    let
      numberOfElements  = 1000000
      maxValue          = 1152921504606846976 -- 2^60
      seqThreshold      = 2048
      randomSeed        = 1024
    in
      (new QuickSortActor(null, initial(), seqThreshold, numberOfElements))!sort(randomInitialArray(numberOfElements, randomSeed, maxValue))
      println("Done!")
    end
  end
end
